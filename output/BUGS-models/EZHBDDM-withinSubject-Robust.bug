model{
          bound_mean ~ dnorm(2.5,pow(2,-2))T(0.1,)
          nondt_mean ~ dnorm(0.5,pow(0.2,-2))T(0.05,)
          drift_mean ~ dnorm(0,pow(3,-2))
          bound_sdev ~ dunif(0.01,2)
          nondt_sdev ~ dunif(0.01,0.5)
          drift_sdev ~ dunif(0.01,2)
          betaweight ~ dnorm(0,pow(1,-2))T(-3,3)

  
                  for(p in 1:nParticipants) {
                      bound[p] ~ dnorm(bound_mean, pow(bound_sdev, -2))T(1e-04,)
                      nondt[p] ~ dnorm(nondt_mean, pow(nondt_sdev, -2))T(1e-04,)
                      for(j in 1:2){
                          drift[p,j] ~ dnorm(drift_mean+betaweight*(j-1), pow(drift_sdev, -2))
                      }
                  }

              # Forward equations from EZ Diffusion
              for (k in 1:length(meanRT)) {
                  ey[k]  = exp(-bound[P[k]] * drift[P[k],(X[k])+1])
                  Pc[k]  = 1 / (1 + ey[k])
                  PRT[k] = 2 * pow(drift[P[k],(X[k]+1)], 3) / bound[P[k]] * pow(ey[k] + 1, 2) / (2 * -bound[P[k]] * drift[P[k],(X[k]+1)] * ey[k] - ey[k]*ey[k] + 1)
                  MDT[k] = (bound[P[k]] / (2 * drift[P[k],(X[k]+1)])) * (1 - ey[k]) / (1 + ey[k])
                  MRT[k] = MDT[k] + nondt[P[k]]

              # Loss functions using MRT, PRT, and Pc
                 correct[k] ~ dbin(Pc[k], nTrialsPerCondition)
                 medianRT[k]  ~ dnorm(MRT[k], PRT[k] * nTrialsPerCondition)
                 iqrVarRT[k]   ~ dnorm(1/PRT[k], 0.5*(nTrialsPerCondition-1) * PRT[k] * PRT[k])
              }
 }
